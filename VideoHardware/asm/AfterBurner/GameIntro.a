!zn
;ksms1CarrierScale_SmallestValue = 12	; At 13.7 MHz
;ksms1CarrierScale_SmallestValue = 15	; At 12.096 MHZ
ksms1CarrierScale_SmallestValue = 12	; At 12.096 MHZ and with dual sprites4 layers

InitGameIntro
	jsr Bus20To32Bit1_Init

	; Init state
	+MByteValueToAddress_A 1 , Player_Invulnerable
	+MWordValueToAddress_A LandscapeMapRowsData , LandscapeMapRowsDataAddress
	+MByteValueToAddress_A 0 , LandscapeFlags
	+MByteValueToAddress_A 0 , GroundColours
	+MByteValueToAddress_A 3 , GamePaletteBank
	+MByteValueToAddress_A 0 , EntityEnableRenderPlayerSpecialCase
	+MByteValueToAddress_A 0 , landscapeRotationsFrameRoll
	+MByteValueToAddress_A 0 , playerXPositionOffset
	+MByteValueToAddress_A (ScreenLandscapeNumRollSteps/2)-1 , landscapeRotationsFrame

	jsr Music_GameChoice

	lda #0
	sta smOffsetY1Carrier+1
	lda #$20
	sta sms1CarrierScale+1
	lda #1
	sta landscapeFrameSpeed
	jsr PlayerSpeedHasChangedNotify

	; Engine
!ifndef BuildTitleScreen {
	+MPlaySampleLoop 0 , 32 , kSampleInfo11_start , kSampleInfo11_length , kSampleInfo11_loopStart , kSampleInfo11_looplength , kSampleInfo11_frequency / 2
}
	lda #32
	sta Player_EngineVolume
	+MWordValueToAddress_A kSampleInfo11_frequency / 2 , Player_EngineFrequency
	+MWordValueToAddress_A kSampleInfo11_frequency , Player_EngineTargetFrequency
	; Get ready
!ifndef BuildTitleScreen {
	+MPlaySample 1 , 255 , kSampleInfo8_start , kSampleInfo8_length , kSampleInfo8_frequency
}

	jsr GameShowScreen

	rts


InitGameComplete
	jsr Bus20To32Bit1_Init

	; Init state
	+MWordValueToAddress_A LandscapeMapRowsData , LandscapeMapRowsDataAddress
	+MByteValueToAddress_A 1 , Player_Invulnerable
	+MByteValueToAddress_A 1 , RunningGameComplete
	+MByteValueToAddress_A 0 , LandscapeFlags
	+MByteValueToAddress_A 0 , GroundColours
	+MByteValueToAddress_A 3 , GamePaletteBank
	+MByteValueToAddress_A 1 , LandscapeMapRowsFreezeUpdates
	lda #2
	sta smOffsetY1Carrier+1
	lda #200
	sta sms1CarrierScale+1
	lda #1
	sta landscapeFrameSpeed
	jsr PlayerSpeedHasChangedNotify

	+MByteValueToAddress_A 1 , EntityEnableRenderPlayerSpecialCase
	+MByteValueToAddress_A 0 , landscapeRotationsFrameRoll

	+MByteValueToAddress_A -45 , Player_EngineVolumeChange
	+MWordValueToAddress_A kSampleInfo11_frequency / 2 , Player_EngineTargetFrequency

	jsr Music_Play6

	rts

RunningGameComplete !by 0

.levelCompleteFrames !by 30
RunGameComplete
mainLoop4b
	jsr Video_WaitVBlank
	jsr Video_StartRasterTimers
	; Must happen in the VBlank
	jsr Bus20To32Bit1_Init
	jsr VBlankUpdatePaletteColours
	jsr VBlankUpdateHorizon
	jsr VBlankUpdateScore
	jsr VBlankUpdateText

	; Can happen outside the VBlank
	jsr UpdateText_Poll_PerFrame
	jsr EntitySort

	+MFrameCompensate_30fps_Before ~.frameCompensate1b , .frameCompensate1ob

	jsr Music_CheckTime
;	jsr CarrierUpdateLogic
	jsr TryToLevelAnyRoll
	jsr ReducePlayerXPositionForNoInput
	jsr playerYPositionOffsetSpeedReduce
	jsr playerYPositionOffsetReduce
	jsr UpdateText_Poll
	jsr PlayerEngineSoundControl
	jsr UpdateLandscapeFrame
	jsr EntityUpdateObjects
	jsr LandscapeAnimate

	+MFrameCompensate_30fps_After  .frameCompensate1b , ~.frameCompensate1ob

	jsr ResetInterfaceAndStartSendingScaledSprites
	jsr MissionCompleteDisplayRenderSpritesDirectMode
	jsr RadarDisplayRenderSpritesDirectMode
	jsr SpeedDisplayRenderSpritesDirectMode
	jsr EntityRenderSpritesDirectMode
;	lda EntityEnableRenderPlayerSpecialCase
;	bne .skipPlayerRender2
;	jsr RenderSpritesFakePlayer2
;.skipPlayerRender2
!ifdef Debug_RenderTimeout {
	jsr RenderTimeout
}
	jsr TerminateScaledSpritesAndSignalReady
	jsr ResetInterfaceAndStartSendingScaledSprites2
;	jsr RenderSpritesCarrier
	jsr PlayerPositionToForHorizonAndLandscape
	jsr LandscapeRender
!ifdef Debug_RenderTimeout {
	jsr RenderTimeout2
}
	jsr TerminateScaledSpritesAndSignalReady2

	dec .levelCompleteFrames
	bne mainLoop4b
	+MByteValueToAddress_A -95 , Player_EngineVolumeChange
	jmp mainLoop4

RunGameIntro
	+MByteValueToAddress_A 0 , Video_WaitVBlank_FramesPassed

mainLoop4
	jsr Video_WaitVBlank
	jsr Video_StartRasterTimers
	; Must happen in the VBlank
	jsr Bus20To32Bit1_Init
	jsr VBlankUpdatePaletteColours
	jsr VBlankUpdateHorizon
	jsr VBlankUpdateScore
	jsr VBlankUpdateText

	; Can happen outside the VBlank
	jsr EntitySort

	+MFrameCompensate_30fps_Before ~.frameCompensate1 , .frameCompensate1o

	jsr Music_CheckTime
	jsr CarrierUpdateLogic
	jsr LandscapeAnimate

	+MFrameCompensate_30fps_After  .frameCompensate1 , ~.frameCompensate1o

	jsr ResetInterfaceAndStartSendingScaledSprites
	lda RunningGameComplete
	beq .skipMissionComplete
	jsr MissionCompleteDisplayRenderSpritesDirectMode
.skipMissionComplete
	jsr RadarDisplayRenderSpritesDirectMode
	jsr SpeedDisplayRenderSpritesDirectMode
	jsr EntityRenderSpritesDirectMode
	lda RunningGameComplete
	bne .skipPlayerRender
	jsr RenderSpritesFakePlayer
	jmp .skipPlayerRender2
.skipPlayerRender
	lda EntityEnableRenderPlayerSpecialCase
	bne .skipPlayerRender2
	jsr RenderSpritesFakePlayer2
.skipPlayerRender2
!ifdef Debug_RenderTimeout {
	jsr RenderTimeout
}
	jsr TerminateScaledSpritesAndSignalReady
	jsr ResetInterfaceAndStartSendingScaledSprites2
	jsr RenderSpritesCarrier
	jsr PlayerPositionToForHorizonAndLandscape
	jsr LandscapeRender
!ifdef Debug_RenderTimeout {
	jsr RenderTimeout2
}
	jsr TerminateScaledSpritesAndSignalReady2

	lda smOffsetY1Carrier+1
	cmp #250
	bcc mainLoop4
	rts


RenderSpritesFakePlayer

	; Render a fake player with scale based on the carrier
	lda sms1CarrierScale+1
	clc
	adc #32-ksms1CarrierScale_SmallestValue
	tax
	+MWordValueToAddressLoHi_A ScreenLandscapeOriginXPos , zeroPage_Temp5 , zeroPage_Temp6
	+MWordValueToAddressLoHi_A 172 , zeroPage_Temp4 , zeroPage_Temp7
	lda smOffsetY1Carrier+1
	lsr
	lsr
	sta zeroPage_Temp8
	+MSubU8AddrToAddr16 zeroPage_Temp8 , zeroPage_Temp4 , zeroPage_Temp7
	lda kVarsEmitSpriteFrame_t_380_0_0_0_tileWidthScaleTab-1,x
	lsr
	sta zeroPage_Temp9
	lda zeroPage_Temp5
	sec
	sbc zeroPage_Temp9
	sta zeroPage_Temp5
	lda zeroPage_Temp6
	sbc #0
	sta zeroPage_Temp6
	; Y Pos
	lda zeroPage_Temp4
	sec
	sbc kVarsEmitSpriteFrame_t_380_0_0_0_tileHeightScaleTab-1,x
	sta zeroPage_Temp4
	lda zeroPage_Temp7
	sbc #0
	sta zeroPage_Temp7
	jsr MSBCalcWith6n7

	lda sms1CarrierScale+1
	cmp #18
	+lbcs .renderOtherPlayer
	+MObjectDrawWithScaleSizeCheck kVarsEmitSpriteFrame_t_6_0_0_0_colour , kVarsEmitSpriteFrame_t_6_0_0_0_tileWidthScaleTab , kVarsEmitSpriteFrame_t_6_0_0_0_tileHeightScaleTab , kVarsEmitSpriteFrame_t_6_0_0_0_tileAddress , kVarsEmitSpriteFrame_t_6_0_0_0_tileWidth
	jmp .renderOtherPlayerOver
.renderOtherPlayer
	+MObjectDrawWithScaleSizeCheck kVarsEmitSpriteFrame_t_380_0_0_0_colour , kVarsEmitSpriteFrame_t_380_0_0_0_tileWidthScaleTab , kVarsEmitSpriteFrame_t_380_0_0_0_tileHeightScaleTab , kVarsEmitSpriteFrame_t_380_0_0_0_tileAddress , kVarsEmitSpriteFrame_t_380_0_0_0_tileWidth
.renderOtherPlayerOver

	rts

.playerSpecialCaseScale !by $20
.playerSpecialCaseYPosTweak !by 0
RenderSpritesFakePlayer2
	ldx .playerSpecialCaseScale

	+MWordValueToAddressLoHi_A ScreenLandscapeOriginXPos , zeroPage_Temp5 , zeroPage_Temp6
	+MWordValueToAddressLoHi_A 132 , zeroPage_Temp4 , zeroPage_Temp7
	lda #0
	sta zeroPage_Temp3
	lda zeroPage_Temp4
	clc
	adc .playerSpecialCaseYPosTweak
	sta zeroPage_Temp4
	lda kVarsEmitSpriteFrame_t_380_0_0_0_tileWidthScaleTab-1,x
	lsr
	sta zeroPage_Temp9
	lda zeroPage_Temp5
	sec
	sbc zeroPage_Temp9
	sta zeroPage_Temp5
	lda zeroPage_Temp6
	sbc #0
	sta zeroPage_Temp6
	; Y Pos
	lda zeroPage_Temp4
	sec
	sbc kVarsEmitSpriteFrame_t_380_0_0_0_tileHeightScaleTab-1,x
	sta zeroPage_Temp4
	lda zeroPage_Temp7
	sbc #0
	sta zeroPage_Temp7
	jsr MSBCalcWith6n7

	cpx #40
	+lbcc .renderOtherPlayer3

	cpx #60
	+lbcs .renderOtherPlayer2
	+MObjectDrawWithScaleSizeCheck kVarsEmitSpriteFrame_t_6_0_0_0_colour , kVarsEmitSpriteFrame_t_6_0_0_0_tileWidthScaleTab , kVarsEmitSpriteFrame_t_6_0_0_0_tileHeightScaleTab , kVarsEmitSpriteFrame_t_6_0_0_0_tileAddress , kVarsEmitSpriteFrame_t_6_0_0_0_tileWidth
	jmp .renderOtherPlayerOver2
.renderOtherPlayer3
	+MObjectDrawWithScaleSizeCheck kVarsEmitSpriteFrame_t_380_0_0_0_colour , kVarsEmitSpriteFrame_t_380_0_0_0_tileWidthScaleTab , kVarsEmitSpriteFrame_t_380_0_0_0_tileHeightScaleTab , kVarsEmitSpriteFrame_t_380_0_0_0_tileAddress , kVarsEmitSpriteFrame_t_380_0_0_0_tileWidth
	jmp .renderOtherPlayerOver2
.renderOtherPlayer2
	+MObjectDrawWithScaleSizeCheck kVarsEmitSpriteFrame_t_10_0_0_0_colour , kVarsEmitSpriteFrame_t_10_0_0_0_tileWidthScaleTab , kVarsEmitSpriteFrame_t_10_0_0_0_tileHeightScaleTab , kVarsEmitSpriteFrame_t_10_0_0_0_tileAddress , kVarsEmitSpriteFrame_t_10_0_0_0_tileWidth
	jmp .renderOtherPlayerOver2
.renderOtherPlayerOver2
	rts


RenderSpritesCarrier
sms1CarrierScale	ldx #$20
	; X calc
	+MWordValueToAddressLoHi_A ScreenLandscapeOriginXPos , zeroPage_Temp5 , zeroPage_Temp6
	; Simple 16 bit calculation to center the sprites
	; Accumulate in steps
!for .i , 2 {
	lda zeroPage_Temp5
	sec
	sbc kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4-1,x
	sta zeroPage_Temp5
	lda zeroPage_Temp6
	sbc #0
	sta zeroPage_Temp6
}
	; Y calc
	+MWordValueToAddressLoHi_A ScreenLandscapeOriginYPos + 50 , zeroPage_Temp4 , zeroPage_Temp7
smOffsetY1Carrier	lda #0
	+MAddAToAddr16 zeroPage_Temp4 , zeroPage_Temp7
	lda zeroPage_Temp4
	sec
	sbc kVarsEmitSpriteFrame_zt_715_0_0_0_tileHeightScaleTabDiv2-1,x
	sta zeroPage_Temp4
	lda zeroPage_Temp7
	sbc #0
	sta zeroPage_Temp7

	jsr MSBCalcWith6n7
	bcs .noDraw1
	+MObjectDrawWithScaleSizeCheck kVarsEmitSpriteFrame_t_715_0_0_0_colour , kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4 , kVarsEmitSpriteFrame_zt_715_0_0_0_tileHeightScaleTabDiv2 , kVarsEmitSpriteFrame_t_715_0_0_0_tileAddress , kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth
.noDraw1

!for .i , 3 {
!set thisI = .i
!zn {
	; Simple 16 bit calculation to center the sprites
	lda zeroPage_Temp5
	clc
	adc kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4-1,x
	sta zeroPage_Temp5
	lda zeroPage_Temp6
	adc #0
	sta zeroPage_Temp6

	jsr MSBCalcWith6n7
	bcs .noDraw2
	+MObjectDrawWithScaleSizeCheck kVarsEmitSpriteFrame_t_715_0_0_0_colour , kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4 , kVarsEmitSpriteFrame_zt_715_0_0_0_tileHeightScaleTabDiv2 , kVarsEmitSpriteFrame_t_715_0_0_0_tileAddress + ((kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth/4)*thisI) , kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth
.noDraw2
}
}

	lda smOffsetY1Carrier+1
	cmp #85	; Tweaked
	+lbcs .noSecondRow1

	; Next row
	; Y calc
	lda zeroPage_Temp4
	clc
	adc kVarsEmitSpriteFrame_zt_715_0_0_0_tileHeightScaleTabDiv2-1,x
	sta zeroPage_Temp4
	lda zeroPage_Temp7
	adc #0
	sta zeroPage_Temp7

	; X calc
	+MWordValueToAddressLoHi_A ScreenLandscapeOriginXPos , zeroPage_Temp5 , zeroPage_Temp6
	; Simple 16 bit calculation to center the sprites
	; Accumulate in steps
!for .i , 2 {
	lda zeroPage_Temp5
	sec
	sbc kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4-1,x
	sta zeroPage_Temp5
	lda zeroPage_Temp6
	sbc #0
	sta zeroPage_Temp6
}

	jsr MSBCalcWith6n7
	bcs .noDraw12
	+MObjectDrawWithScaleSizeCheck kVarsEmitSpriteFrame_t_715_0_0_0_colour , kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4 , kVarsEmitSpriteFrame_zt_715_0_0_0_tileHeightScaleTabDiv2 , kVarsEmitSpriteFrame_t_715_0_0_0_tileAddress + (kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth * kVarsEmitSpriteFrame_t_715_0_0_0_tileHeight/2) , kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth
.noDraw12

!for .i , 3 {
!set thisI = .i
!zn {
	; Simple 16 bit calculation to center the sprites
	lda zeroPage_Temp5
	clc
	adc kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4-1,x
	sta zeroPage_Temp5
	lda zeroPage_Temp6
	adc #0
	sta zeroPage_Temp6

	jsr MSBCalcWith6n7
	bcs .noDraw22
	+MObjectDrawWithScaleSizeCheck kVarsEmitSpriteFrame_t_715_0_0_0_colour , kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4 , kVarsEmitSpriteFrame_zt_715_0_0_0_tileHeightScaleTabDiv2 , kVarsEmitSpriteFrame_t_715_0_0_0_tileAddress + (kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth * kVarsEmitSpriteFrame_t_715_0_0_0_tileHeight/2) + ((kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth/4)*thisI) , kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth
.noDraw22
}
}
.noSecondRow1
	rts



!macro MGameIntroAllocateSmokeWith .xpos {
	jsr EntityFindFreeSlot
	bcs .noFreeSlot1
	lda #.xpos
	sta EntityXPos,x
	lda #-64
	sec
	sbc playerYPositionOffset
	+M_ASR
	sta EntityYPos,x
	lda #48	; Should be defaultNeutralScaleIs, but in the game intro mode these smokes are a special case, tweaked to look really nice
	sta EntityZPos,x
	lda #10
	sta EntityScaleTweak,x
	lda #<Render_WhiteSmoke
	sta EntityTypeLo,x
	lda #>Render_WhiteSmoke
	sta EntityTypeHi,x
	lda #0
	sta EntityTypeIsRotating,x
.noFreeSlot1
}

CarrierUpdateLogic
	lda RunningGameComplete
	beq .doIntroLogic

	jsr TryToLevelAnyRoll
	jsr ReducePlayerXPositionForNoInput
	jsr playerYPositionOffsetSpeedReduce
	jsr playerYPositionOffsetReduce

.smmt1 lda #4
	bne .gotGoodPosition
	lda #8
	sta .smmt1+1
	lda smOffsetY1Carrier+1
	beq .gotGoodPosition
	dec smOffsetY1Carrier+1
.gotGoodPosition
	dec .smmt1+1

	lda sms1CarrierScale+1
	cmp #24
	beq .gotGoodScale
	dec sms1CarrierScale+1
	jmp .gotGoodScale2
.gotGoodScale
	+MByteValueToAddress_A 0 , EntityEnableRenderPlayerSpecialCase

.smst2 lda #0
	bne .smst2o
	lda #2
	sta .smst2+1
	lda .playerSpecialCaseScale
	cmp #90
	+lbeq mainLoop_toMissionComplete
	inc .playerSpecialCaseScale
.smst2o
	dec .smst2+1
.gotGoodPlayerScale

.smst1 lda #0
	bne .smst1o
	lda #3
	sta .smst1+1
	lda .playerSpecialCaseYPosTweak
	cmp #15
	beq .gotGoodTweak
	inc .playerSpecialCaseYPosTweak
.smst1o
	dec .smst1+1
.gotGoodTweak
.gotGoodScale2

	lda #1
	sta landscapeFrameSpeed
	jsr PlayerSpeedHasChangedNotify
	jsr UpdateText_Poll
	jsr PlayerEngineSoundControl
	jsr UpdateLandscapeFrame
	jsr EntityUpdateObjects

	rts

.doIntroLogic
	lda sms1CarrierScale+1
	cmp #18	; Trigger transition
	bcc .noMoreScale1

	+MAddU8ToAddr16 3 , Player_EngineFrequency , Player_EngineFrequency + 1
	+MSampleSetFrequency_fromAddr16 0 , Player_EngineFrequency

.smtt1	lda #10
	bne .notTime1
	lda #4
	sta .smtt1+1
	dec sms1CarrierScale+1
;	inc smOffsetY1Carrier+1
	lda #1
	sta landscapeFrameSpeed
	jsr PlayerSpeedHasChangedNotify
	jsr UpdateText_Poll_PerFrame
	jsr UpdateText_Poll
	jsr PlayerEngineSoundControl
	jsr UpdateLandscapeFrame
	jsr EntityUpdateObjects


.notTime1
	dec .smtt1+1

	rts

.noMoreScale1
	+MAddU8ToAddr16 12 , Player_EngineFrequency , Player_EngineFrequency + 1
	lda Player_EngineVolume
	cmp #250
	bcs .enoughVolume
	inc Player_EngineVolume
	inc Player_EngineVolume
.enoughVolume
	+MSampleSetFrequency_fromAddr16 0 , Player_EngineFrequency
	+MSampleSetVolume_fromAddr8 0 , Player_EngineVolume


	lda smOffsetY1Carrier+1
	clc
	adc #3
	sta smOffsetY1Carrier+1

.everySoOften1	lda #4
	+lbne .everySoOften1Over

	lda sms1CarrierScale+1
	cmp #ksms1CarrierScale_SmallestValue	; Beyond this, the maths breaks down a lot
	bcc .noMoreScale2
	dec sms1CarrierScale+1

.noMoreScale2

	lda #4
	sta .everySoOften1+1

	lda smOffsetY1Carrier+1
	cmp #150
	bcs .decreaseSpeed1

	+MGameIntroAllocateSmokeWith -10
	+MGameIntroAllocateSmokeWith 10

	lda landscapeFrameSpeed
	cmp #7
	bcs .noMoreSpeed1
	inc landscapeFrameSpeed
	jmp .noMoreSpeed1
.decreaseSpeed1
	lda landscapeFrameSpeed
	cmp #3
	bcc .noMoreSpeed1
	dec landscapeFrameSpeed
.noMoreSpeed1

.everySoOften1Over
	dec .everySoOften1+1
	jsr UpdateLandscapeFrame
	jsr EntityUpdateObjects

	lda playerYPositionOffset
	cmp #-25
	+IfRegLessThanVal .noMorePitch1
	dec playerYPositionOffset
.noMorePitch1

	rts

MSBCalcWith6n7
	lda zeroPage_Temp6
	beq .okToDraw1
	cmp #1
	beq .okToDraw1
	cmp #-1
	beq .okToDraw1
	jmp .rejectDraw
.okToDraw1
	lda zeroPage_Temp7
	beq .okToDraw2
	cmp #1
	beq .okToDraw2
	cmp #-1
	beq .okToDraw2
	jmp .rejectDraw
.okToDraw2
	; MSB calc
	lda #0
	ldy zeroPage_Temp6
	beq .not1
	ora #kBus24Bit_Sprites2_MSBX
.not1
	ldy zeroPage_Temp7
	beq .not2
	ora #kBus24Bit_Sprites2_MSBY
.not2
	sta zeroPage_Temp3
	clc
	rts
.rejectDraw
	sec
	rts

; Renamed "t_715" to be "zt_715" so that the "ScaleTab" references are considered for optimisation, since the name won't match
kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4 = kVarsEmitSpriteFrame_t_1122_0_0_0_tileWidthScaleTab
+MAssertEquals kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth/4 , kVarsEmitSpriteFrame_t_1122_0_0_0_tileWidth
;kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv4
;	+MGenerateSprite4ScaleTable kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth/4

kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv2 = kVarsEmitSpriteFrame_t_113_0_0_0_tileHeightScaleTab
+MAssertEquals kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth/2 , kVarsEmitSpriteFrame_t_113_0_0_0_tileHeight
;kVarsEmitSpriteFrame_zt_715_0_0_0_tileWidthScaleTabDiv2
;	+MGenerateSprite4ScaleTable kVarsEmitSpriteFrame_t_715_0_0_0_tileWidth/2

kVarsEmitSpriteFrame_zt_715_0_0_0_tileHeightScaleTabDiv2 = kVarsEmitSpriteFrame_t_635_0_0_0_tileHeightScaleTab
+MAssertEquals kVarsEmitSpriteFrame_t_715_0_0_0_tileHeight/2 , kVarsEmitSpriteFrame_t_635_0_0_0_tileHeight
;kVarsEmitSpriteFrame_zt_715_0_0_0_tileHeightScaleTabDiv2
;	+MGenerateSprite4ScaleTable kVarsEmitSpriteFrame_t_715_0_0_0_tileHeight/2
