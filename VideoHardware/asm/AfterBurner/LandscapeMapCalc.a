!zn
UpdateLandscapeFrameInternal
	lda landscapeFrame
	clc
	adc landscapeFrameSpeed
	sta landscapeFrame
	cmp #8
	+lbcc .noNewRow
	and #7
	sta landscapeFrame

	; Update the palette bank for the current map row type
	ldy #kLandscapeMapRowsData_offset_Palette
	+MAddr16ToAddr16_A LandscapeMapRowsDataAddress , zeroPage_Temp18
	lda (zeroPage_Temp18),y
	sta GamePaletteBank
	ldy #kLandscapeMapRowsData_offset_Ground
	lda (zeroPage_Temp18),y
	sta GroundColours
	ldy #kLandscapeMapRowsData_offset_Flags
	lda (zeroPage_Temp18),y
	sta LandscapeFlags

	; Calculate new map data into the row we are just about to move over
	jsr Rand
	eor #%10110101
	sta .sme1+1
	; This adjusts the row that gets updated to the new map row type is introduced right at the far visible edge of the horizon
	lda LandscapeMapRowOffset
	sec
	sbc #16*7
	tax
	lda #17	; Add one to remove any previous data due to banking or moving right in the level map
	sta zeroPage_Temp0
.umd1
	jsr Rand
.sme1	eor #%10110101
	and #7
	clc
	adc #kLandscapeMapRowsData_offset_ObjectTypes
	tay
	lda (zeroPage_Temp18),y
	sta LandscapeMap,x
	inx
	inc .sme1+1
	ror .sme1+1
	dec zeroPage_Temp0
	bne .umd1

	lda LandscapeMapRowOffset
	clc
	ldy landscapeRotationsFrame
	adc landscapeRotationsFrameToOffset,y
	sta LandscapeMapRowOffset
	and #$f0
	bne .noAdvanceLandscapeMapRowsDataAddress

	; Advance through the map data row types with each whole landscape pass
	+MAddU8ToAddr16 kLandscapeMapRowsData_size , LandscapeMapRowsDataAddress , LandscapeMapRowsDataAddress + 1
	lda LandscapeMapRowsDataAddress
	cmp #<LandscapeMapRowsDataEnd
	bne .noAdvanceLandscapeMapRowsDataAddress
	lda LandscapeMapRowsDataAddress+1
	cmp #>LandscapeMapRowsDataEnd
	bne .noAdvanceLandscapeMapRowsDataAddress
	+MWordValueToAddress_A LandscapeMapRowsData , LandscapeMapRowsDataAddress

.noAdvanceLandscapeMapRowsDataAddress


.noNewRow
	rts


; Map row data...
; 1 Byte palette bank -> GamePaletteBank
; 1 byte -> GroundColours
; 1 byte -> LandscapeFlags
; 8 bytes of object types that can be randomly selected for every entire cycle through the whole range of the landscape with LandscapeMapRowOffset

kLandscapeMapRowsData_offset_Palette		= 0
kLandscapeMapRowsData_offset_Ground			= 1
kLandscapeMapRowsData_offset_Flags			= 2
kLandscapeMapRowsData_offset_ObjectTypes	= 3
kLandscapeMapRowsData_size					= 11

kLandscapeMapRowsData_flags_doLanding	= %00000001
kLandscapeMapRowsData_flags_doCanyon	= %00000010

!macro LandscapeMapRowsData_PalettesFlags .palette , .ground , .flags {
	!by .palette , .ground , .flags
}

!macro LandscapeMapRowsData_Objects .o0 , .o1 , .o2 , .o3 , .o4 , .o5 , .o6 , .o7 {
	!by .o0 , .o1 , .o2 , .o3 , .o4 , .o5 , .o6 , .o7
}

LandscapeMapRowsData
; Carrier palette
+LandscapeMapRowsData_PalettesFlags 3	,0	, 0
+LandscapeMapRowsData_Objects 6,6,6,6,6,6,6,6
+LandscapeMapRowsData_PalettesFlags 3	,0	, 0
+LandscapeMapRowsData_Objects 6,6,6,6,6,6,6,6
+LandscapeMapRowsData_PalettesFlags 3	,4	, 0
+LandscapeMapRowsData_Objects 11,11,11,11,11,11,11,11
+LandscapeMapRowsData_PalettesFlags 3	,4	, 0
+LandscapeMapRowsData_Objects 11,11,11,11,11,11,11,11

+LandscapeMapRowsData_PalettesFlags 0	,0	, 0
+LandscapeMapRowsData_Objects 6,6,6,1,6,6,6,6
+LandscapeMapRowsData_PalettesFlags 0	,0	, 0
+LandscapeMapRowsData_Objects 6,6,6,6,6,1,2,3

+LandscapeMapRowsData_PalettesFlags 0	,1	, 0
+LandscapeMapRowsData_Objects 6,1,2,3,4,5,0,1
+LandscapeMapRowsData_PalettesFlags 0	,2	, 0
+LandscapeMapRowsData_Objects 1,2,3,4,5,0,1,2
+LandscapeMapRowsData_PalettesFlags 0	,3	, 0
+LandscapeMapRowsData_Objects 1,1,1,1,1,0,0,0

+LandscapeMapRowsData_PalettesFlags 1	,3	, kLandscapeMapRowsData_flags_doLanding
+LandscapeMapRowsData_Objects 1,1,1,1,1,0,0,0
+LandscapeMapRowsData_PalettesFlags 1	,2	, 0
+LandscapeMapRowsData_Objects 0,1,2,8,8,8,0,8
+LandscapeMapRowsData_PalettesFlags 1	,3	, 0
+LandscapeMapRowsData_Objects 1,1,0,1,1,0,0,0
+LandscapeMapRowsData_PalettesFlags 1	,3	, 0
+LandscapeMapRowsData_Objects 1,1,0,1,1,0,0,0

+LandscapeMapRowsData_PalettesFlags 0	,2	, 0
+LandscapeMapRowsData_Objects 2,2,7,2,1,7,7,0
+LandscapeMapRowsData_PalettesFlags 0	,3	, 0
+LandscapeMapRowsData_Objects 2,7,2,2,7,0,0,0
+LandscapeMapRowsData_PalettesFlags 0	,2	, 0
+LandscapeMapRowsData_Objects 3,3,3,3,0,0,0,0
+LandscapeMapRowsData_PalettesFlags 0	,1	, 0
+LandscapeMapRowsData_Objects 3,3,3,3,4,0,0,0
+LandscapeMapRowsData_PalettesFlags 0	,2	, 0
+LandscapeMapRowsData_Objects 4,4,7,4,0,0,0,0

+LandscapeMapRowsData_PalettesFlags 0	,3	, 0
+LandscapeMapRowsData_Objects 1,1,0,1,1,0,2,2
+LandscapeMapRowsData_PalettesFlags 2	,1	, kLandscapeMapRowsData_flags_doCanyon
+LandscapeMapRowsData_Objects 12,0,0,0,14,0,15,0
+LandscapeMapRowsData_PalettesFlags 2	,2	, kLandscapeMapRowsData_flags_doCanyon
+LandscapeMapRowsData_Objects 0,13,0,14,0,15,0,0
+LandscapeMapRowsData_PalettesFlags 2	,2	, kLandscapeMapRowsData_flags_doCanyon
+LandscapeMapRowsData_Objects 0,13,0,0,0,14,0,0
+LandscapeMapRowsData_PalettesFlags 2	,2	, kLandscapeMapRowsData_flags_doCanyon
+LandscapeMapRowsData_Objects 0,13,0,12,0,14,0,0
+LandscapeMapRowsData_PalettesFlags 2	,2	, kLandscapeMapRowsData_flags_doCanyon
+LandscapeMapRowsData_Objects 0,0,0,14,0,15,0,0
+LandscapeMapRowsData_PalettesFlags 2	,3	, 0
+LandscapeMapRowsData_Objects 1,1,2,1,1,0,0,0
+LandscapeMapRowsData_PalettesFlags 0	,3	, 0
+LandscapeMapRowsData_Objects 4,1,2,1,1,3,2,0

+LandscapeMapRowsData_PalettesFlags 0	,2	, 0
+LandscapeMapRowsData_Objects 4,4,4,7,0,0,0,0
+LandscapeMapRowsData_PalettesFlags 0	,1	, 0
+LandscapeMapRowsData_Objects 4,4,4,5,5,7,0,0
+LandscapeMapRowsData_PalettesFlags 0	,1	, 0
+LandscapeMapRowsData_Objects 5,5,5,5,0,0,0,0
+LandscapeMapRowsData_PalettesFlags 0	,1	, 0
+LandscapeMapRowsData_Objects 6,1,2,3,4,5,0,1
+LandscapeMapRowsData_PalettesFlags 0	,0	, 0
+LandscapeMapRowsData_Objects 6,6,6,6,6,1,2,3
LandscapeMapRowsDataEnd
