!zn
InitGamePlayerExplode
!ifndef Debug_Invulnerable {
	lda GamePlayerExplode_ExplodeFrame
	beq .doExplodeNow
}

	rts
.doExplodeNow

	lda GamePlayerExplode_Winged
	beq .notWinged
	inc TriggerSamplePlay_Explosion1
	inc TriggerSamplePlay_Explosion2
	inc TriggerSamplePlay_Explosion3
.notWinged

	+MByteValueToAddress_A 0 , landscapeRotationsFrameRoll
	+MByteValueToAddress_A 1 , LandscapeMapRowsFreezeUpdates
	+MAlsoToAddress_A GamePlayerExplode_ExplodeFrame
	+MByteValueToAddress_A 125 , Player_Invulnerable

	dec GamePlayerLives
	bpl .someLives
	; Exit the canyon if it's running
	+MByteValueToAddress_A 0 , LandscapeFlags
.someLives

	; Remove the lives indicator in the screen status
	lda GamePlayerLives
	bmi .o1
	+MByteValueToAddress_A 1 , VBlankUpdateCharAtFlag
	+MByteValueToAddress_A 0 , VBlankUpdateCharAt
	+MWordValueToAddressLoHi_A kBus24Bit_CharScreenMap + Text_Lives_Pos , VBlankUpdateCharAtLo , VBlankUpdateCharAtHi
	lda GamePlayerLives
	+MAddAToAddr16 VBlankUpdateCharAtLo , VBlankUpdateCharAtHi
.o1

	rts

RunGamePlayerExplode
	+MByteValueToAddress_A 125 , Player_Invulnerable

	+MByteValueToAddress_A -25 , Player_EngineVolumeChange
	+MWordValueToAddress_A kSampleInfo11_frequency / 4 , Player_EngineTargetFrequency
	; Gradual slowdown
	lda landscapeFrameSpeed
	cmp #1
	+IfRegLessThanOrEqualToVal .noMoreSlowTab1
	dec landscapeFrameSpeed
	jsr PlayerSpeedHasChangedNotify
.noMoreSlowTab1

	lda GamePlayerExplode_Winged
	beq .pitchLandscapeUp
	inc GamePlayerExplode_Winged
	lda playerYPositionOffset
	cmp #50
	+IfRegGreaterThanOrEqualToVal .noMorePitch2
	inc playerYPositionOffset
	jmp .noMorePitch2
.pitchLandscapeUp
	lda GamePlayerExplode_ExplodeFrame
	cmp #100
	bcc .doPitchUp
	lda playerYPositionOffset
	bpl .noMorePitch2
	inc playerYPositionOffset
	jmp .noMorePitch2
.doPitchUp
	; Horizon pitch
	lda playerYPositionOffset
	cmp #-50
	+IfRegLessThanOrEqualToVal .noMorePitch2
	dec playerYPositionOffset
.noMorePitch2

	jsr TryToLevelAnyRoll
	jsr PlayerPositionToForHorizonAndLandscape
	jsr ReducePlayerXPositionForNoInput

	lda GamePlayerExplode_Winged
	cmp #130
	bcc .notLandscapeYet
	lda LandscapeMapRowOffset
	sbc #(24*8)-6
	tay
	lda #19	; or 19
	sta LandscapeMap,y
.notLandscapeYet

	lda GamePlayerExplode_Winged
	cmp #240
	beq .doNextState
	cmp #241
	bcs .exitExplodingPlayerState
	lda GamePlayerExplode_Winged
	+lbne GamePlayerExplode_stillExplodingWinged
	inc GamePlayerExplode_ExplodeFrame
	lda GamePlayerExplode_ExplodeFrame
	cmp #150
	bcs .exitExplodingPlayerState
	cmp #100
	bcc .stillExploding
	cmp #101
	bne .stillExploding
.doNextState
	lda GamePlayerLives
	bmi .stillExploding
	+MByteValueToAddress_A 140 , Text_Ready_Flash
	+MByteValueToAddress_A 50 , Player_EngineVolumeChange
	+MWordValueToAddress_A kSampleInfo11_frequency / 2 , Player_EngineTargetFrequency
	+MByteValueToAddress_A 1 , landscapeFrameSpeed
.stillExploding

	rts

.exitExplodingPlayerState
	lda GamePlayerLives
	bmi .exitWithoutSpawn

	+MByteValueToAddress_A 50 , Player_EngineVolumeChange
	+MWordValueToAddress_A kSampleInfo11_frequency / 2 , Player_EngineTargetFrequency
	+MByteValueToAddress_A 2 , landscapeFrameSpeed
	+MByteValueToAddress_A defaultNeutralScaleIs , EntityZPos

	; Do not go quickly if it is the canyon section
	lda #kLandscapeMapRowsData_flags_doCanyon
	bit LandscapeFlags
	beq .notCanyon
	+MByteValueToAddress_A 1 , landscapeFrameSpeed
.notCanyon

	+MByteValueToAddress_A 0 , GamePlayerExplode_ExplodeFrame
	+MAlsoToAddress_A GamePlayerExplode_Winged
	+MAlsoToAddress_A LandscapeMapRowsFreezeUpdates

	rts

.exitWithoutSpawn
	jsr InitGameOver
	jmp RunGameOver
