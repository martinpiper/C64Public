!macro MObjectDrawWithScale .colour , .widthScaleTab , .heightScaleTab , .address , .width {
	; Palette
	lda #.colour
	ora zeroPage_Temp3
	+MBus24Bit_Send8BitValueFromA

	; Y pos and extent
	lda zeroPage_Temp4
	+MBus24Bit_Send8BitValueFromA
	lda .heightScaleTab-1,x
	+MBus24Bit_Send8BitValueFromA

	; X pos and extent
	lda zeroPage_Temp5
	+MBus24Bit_Send8BitValueFromA
	lda .widthScaleTab-1,x
	+MBus24Bit_Send8BitValueFromA

	; Address
	+MBus24Bit_SendLE24BitValue .address

	; Inv scales
	+MBus24Bit_Send8BitValueFromX
	+MBus24Bit_Send8BitValueFromX
	; Stride
	+MBus24Bit_Send8BitValue .width-1
}

!macro MObjectDrawWithScaleFlips .palette , .widthScaleTab , .heightScaleTab , .address , .tileWidth , .tileHeight , .flipX , .flipY {
!if .flipX != 0 {
!set .palette = .palette | kBus24Bit_Video_FlipH
!set .address = .address + .tileWidth - 1
}
!if .flipY != 0 {
!set .palette = .palette | kBus24Bit_Video_FlipV
!set .address = .address + ((.tileHeight-1) * .tileWidth)
}

	; Palette
	lda #.palette
	ora zeroPage_Temp3
	+MBus24Bit_Send8BitValueFromA

	; Y pos and extent
	lda zeroPage_Temp4
	+MBus24Bit_Send8BitValueFromA
	lda .heightScaleTab-1,x
	+MBus24Bit_Send8BitValueFromA

	; X pos and extent
	lda zeroPage_Temp5
	+MBus24Bit_Send8BitValueFromA
	lda .widthScaleTab-1,x
	+MBus24Bit_Send8BitValueFromA

	; Address
	+MBus24Bit_SendLE24BitValue .address

	; Inv scales
	+MBus24Bit_Send8BitValueFromX
	+MBus24Bit_Send8BitValueFromX
	; Stride
	+MBus24Bit_Send8BitValue .tileWidth-1
}

!macro MObjectDrawWithScaleSizeCheck .colour , .widthScaleTab , .heightScaleTab , .address , .width {
	lda .widthScaleTab-1,x
	beq .skipDraw
	lda .heightScaleTab-1,x
	beq .skipDraw
	; Palette
	lda #.colour
	ora zeroPage_Temp3
	+MBus24Bit_Send8BitValueFromA

	; Y pos and extent
	lda zeroPage_Temp4
	+MBus24Bit_Send8BitValueFromA
	lda .heightScaleTab-1,x
	+MBus24Bit_Send8BitValueFromA

	; X pos and extent
	lda zeroPage_Temp5
	+MBus24Bit_Send8BitValueFromA
	lda .widthScaleTab-1,x
	+MBus24Bit_Send8BitValueFromA

	; Address
	+MBus24Bit_SendLE24BitValue .address

	; Inv scales
	+MBus24Bit_Send8BitValueFromX
	+MBus24Bit_Send8BitValueFromX
	; Stride
	+MBus24Bit_Send8BitValue .width-1
.skipDraw
}

!macro MObjectDrawWithScaleSizeCheckWithFlipX .colour , .widthScaleTab , .heightScaleTab , .address , .width {
!set .colour = .colour | kBus24Bit_Video_FlipH
!set .address = .address + .width - 1
	lda .widthScaleTab-1,x
	beq .skipDraw
	lda .heightScaleTab-1,x
	beq .skipDraw
	; Palette
	lda #.colour
	ora zeroPage_Temp3
	+MBus24Bit_Send8BitValueFromA

	; Y pos and extent
	lda zeroPage_Temp4
	+MBus24Bit_Send8BitValueFromA
	lda .heightScaleTab-1,x
	+MBus24Bit_Send8BitValueFromA

	; X pos and extent
	lda zeroPage_Temp5
	+MBus24Bit_Send8BitValueFromA
	lda .widthScaleTab-1,x
	+MBus24Bit_Send8BitValueFromA

	; Address
	+MBus24Bit_SendLE24BitValue .address

	; Inv scales
	+MBus24Bit_Send8BitValueFromX
	+MBus24Bit_Send8BitValueFromX
	; Stride
	+MBus24Bit_Send8BitValue .width-1
.skipDraw
}

!macro MGenerateSprite4ScaleTable .value {
!for .i , UsingMaxHardwareScaleTabValue {
;!set .actualSize = (float(.value * 32) / float(.i))
!if .i <= $20 {
!set .actualSize = int( ((.value << 5) + (.i >> 1)) / (.i) ) ; Rounding up for half a pixel...
} else {
!set .actualSize = ( (float(.value * 32) + (0.5 * 32.0)) / float(.i) ) ; Rounding up for half a pixel...
}
; If the size is too big or too small then ensure it gets written as 0 to flag this case
!if <.actualSize <= 1 {
!set .actualSize = 0
}
!if .actualSize > 255 {
!set .actualSize = 0
}
	!by <.actualSize
; Check the "no scale" value results in the exact sprite size
!if .i = $20 {
!if <.actualSize != .value {
!error "oops"
}
}

}
}


!macro MRenderFixedSpriteVariableWidthInX .palette , .xpos, .ypos , .flipX , .flipY , .address , .tileWidth , .tileHeight {
!if >.xpos != 0 {
!set .palette = .palette | kBus24Bit_Sprites2_MSBX
}
!if >.ypos != 0 {
!set .palette = .palette | kBus24Bit_Sprites2_MSBY
}
!if .flipX != 0 {
!set .palette = .palette | kBus24Bit_Video_FlipH
!set .address = .address + .tileWidth - 1
}
!if .flipY != 0 {
!set .palette = .palette | kBus24Bit_Video_FlipV
!set .address = .address + ((.tileHeight-1) * .tileWidth)
}
	; Shows sprite timeout
	; Palette
	+MBus24Bit_Send8BitValue .palette
	; Y pos and size
	+MBus24Bit_Send8BitValue <.ypos
	+MBus24Bit_Send8BitValue .tileHeight
	; X pos and size
	+MBus24Bit_Send8BitValue <.xpos
	+MBus24Bit_Send8BitValueFromX
	; Address
	+MBus24Bit_SendLE24BitValue .address
	; Inv scales
	+MBus24Bit_Send8BitValue $20
	+MBus24Bit_Send8BitValueFromA	; Note the usage of the previous value in A...
	; Stride
	+MBus24Bit_Send8BitValue .tileWidth-1
}

!macro MRenderFixedSpriteWithStride .palette , .xpos, .ypos , .scaleX , .scaleY , .flipX , .flipY , .address , .tileWidth , .tileHeight , .tileStride {
!if >.xpos != 0 {
!set .palette = .palette | kBus24Bit_Sprites2_MSBX
}
!if >.ypos != 0 {
!set .palette = .palette | kBus24Bit_Sprites2_MSBY
}
!if .flipX != 0 {
!set .palette = .palette | kBus24Bit_Video_FlipH
!set .address = .address + .tileWidth - 1
}
!if .flipY != 0 {
!set .palette = .palette | kBus24Bit_Video_FlipV
!set .address = .address + ((.tileHeight-1) * .tileWidth)
}
	; Shows sprite timeout
	; Palette
	+MBus24Bit_Send8BitValue .palette
	; Y pos and size
	+MBus24Bit_Send8BitValue <.ypos
	+MBus24Bit_Send8BitValue (.tileHeight * $20) / .scaleY
	; X pos and size
	+MBus24Bit_Send8BitValue <.xpos
	+MBus24Bit_Send8BitValue (.tileWidth * $20) / .scaleX
	; Address
	+MBus24Bit_SendLE24BitValue .address
	; Inv scales
	+MBus24Bit_Send8BitValue .scaleY
	+MBus24Bit_Send8BitValue .scaleX
	; Stride
	+MBus24Bit_Send8BitValue .tileStride-1
}

!macro MRenderFixedSprite .palette , .xpos, .ypos , .scaleX , .scaleY , .flipX , .flipY , .address , .tileWidth , .tileHeight {
	+MRenderFixedSpriteWithStride .palette , .xpos, .ypos , .scaleX , .scaleY , .flipX , .flipY , .address , .tileWidth , .tileHeight , .tileWidth
}

!macro MAdjustObjectScaleBy .scale , .widthTab , .heightTab , .widthShifts , .heightShifts {
	; Make the object larger
	txa
	sec
	sbc #.scale
	+lbcc ObjectDrawReturn
	+lbeq ObjectDrawReturn
	tax
	lda .heightTab-1,x
	+lbeq ObjectDrawReturn
!if .heightShifts > 0 {
!for .i , .heightShifts {
	lsr
}
}
	sta .smYTweak2+1
	lda zeroPage_Temp4
	sec
.smYTweak2	sbc #0
	sta zeroPage_Temp4
	bcs .noMSBYTweak2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBY
	sta zeroPage_Temp3
.noMSBYTweak2
	lda .widthTab-1,x
	+lbeq ObjectDrawReturn
!if .widthShifts > 0 {
!for .i , .widthShifts {
	lsr
}
}
	sta .smXTweak2+1
	lda zeroPage_Temp5
	sec
.smXTweak2	sbc #0
	sta zeroPage_Temp5
	bcs .noMSBXTweak2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBX
	sta zeroPage_Temp3
.noMSBXTweak2
}


!macro MAdjustObjectScaleBySmaller .scale , .widthTab , .heightTab , .widthShifts , .heightShifts {
	; Make the object smaller
	txa
	clc
	adc #.scale
	+lbcs ObjectDrawReturn
	tax
	lda .heightTab-1,x
	+lbeq ObjectDrawReturn
!if .heightShifts > 0 {
!for .i , .heightShifts {
	lsr
}
}
	sta .smYTweak2+1
	lda zeroPage_Temp4
	sec
.smYTweak2	sbc #0
	sta zeroPage_Temp4
	bcs .noMSBYTweak2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBY
	sta zeroPage_Temp3
.noMSBYTweak2
	lda .widthTab-1,x
	+lbeq ObjectDrawReturn
!if .widthShifts > 0 {
!for .i , .widthShifts {
	lsr
}
}
	sta .smXTweak2+1
	lda zeroPage_Temp5
	sec
.smXTweak2	sbc #0
	sta zeroPage_Temp5
	bcs .noMSBXTweak2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBX
	sta zeroPage_Temp3
.noMSBXTweak2
}


!macro MAdjustObjectYByNeg .heightTab , .heightShifts {
!if .heightShifts >= 0 {
	lda .heightTab-1,x
;	+lbeq ObjectDrawReturn
!if .heightShifts > 0 {
!for .i , .heightShifts {
	lsr
}
}
	sta .smYTweak2+1
	lda zeroPage_Temp4
	sec
.smYTweak2	sbc #0
	sta zeroPage_Temp4
	bcs .noMSBYTweak2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBY
	sta zeroPage_Temp3
.noMSBYTweak2
}
}

!macro MAdjustObjectYByPos .heightTab , .heightShifts {
!if .heightShifts >= 0 {
	lda .heightTab-1,x
;	+lbeq ObjectDrawReturn
!if .heightShifts > 0 {
!for .i , .heightShifts {
	lsr
}
}
	sta .smYTweak2+1
	lda zeroPage_Temp4
	clc
.smYTweak2	adc #0
	sta zeroPage_Temp4
	bcc .noMSBYTweak2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBY
	sta zeroPage_Temp3
.noMSBYTweak2
}
}


!macro MAdjustObjectXByNeg .widthTab , .widthShifts {
!if .widthShifts >= 0 {
	lda .widthTab-1,x
;	+lbeq ObjectDrawReturn
!if .widthShifts > 0 {
!for .i , .widthShifts {
	lsr
}
}
	sta .smXTweak2+1
	lda zeroPage_Temp5
	sec
.smXTweak2	sbc #0
	sta zeroPage_Temp5
	bcs .noMSBYTweak2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBX
	sta zeroPage_Temp3
.noMSBYTweak2
}
}

!macro MAdjustObjectXByPos .widthTab , .widthShifts {
!if .widthShifts >= 0 {
	lda .widthTab-1,x
;	+lbeq ObjectDrawReturn
!if .widthShifts > 0 {
!for .i , .widthShifts {
	lsr
}
}
	sta .smXTweak2+1
	lda zeroPage_Temp5
	clc
.smXTweak2	adc #0
	sta zeroPage_Temp5
	bcc .noMSBYTweak2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBX
	sta zeroPage_Temp3
.noMSBYTweak2
}
}

!macro MRenderSpriteWithStrideNoScalePositionsX2 .palette , .xpos, .ypos , .flipX , .flipY , .address , .tileWidth , .tileHeight , .tileStride , .xposTweak {
!if .flipX != 0 {
!set .palette = .palette | kBus24Bit_Video_FlipH
!set .address = .address + .tileWidth - 1
}
!if .flipY != 0 {
!set .palette = .palette | kBus24Bit_Video_FlipV
!set .address = .address + ((.tileHeight-1) * .tileWidth)
}
	lda #.palette
	sta zeroPage_Temp3

	lda .xpos
	asl
	sta .xpos
	bcc .noMSBX
	lda #.palette | kBus24Bit_Sprites2_MSBX
	sta zeroPage_Temp3
.noMSBX

!if .xposTweak > 0 {
	lda .xpos
	clc
	adc #.xposTweak
	sta .xpos
	bcc .noMSBX2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBX
	sta zeroPage_Temp3
.noMSBX2
}
!if .xposTweak < 0 {
	lda .xpos
	sec
	sbc #-.xposTweak
	sta .xpos
	bcs .noMSBX2
	lda zeroPage_Temp3
	eor #kBus24Bit_Sprites2_MSBX
	sta zeroPage_Temp3
.noMSBX2
}
	lda .ypos
	asl
	sta .ypos
	bcc .noMSBY
	lda zeroPage_Temp3
	ora #kBus24Bit_Sprites2_MSBY
	sta zeroPage_Temp3
.noMSBY

	; Palette
	lda zeroPage_Temp3
	+MBus24Bit_Send8BitValueFromA

	; Y pos and extent
	lda .ypos
	+MBus24Bit_Send8BitValueFromA
	lda #.tileHeight
	+MBus24Bit_Send8BitValueFromA

	; X pos and extent
	lda .xpos
	+MBus24Bit_Send8BitValueFromA
	lda #.tileWidth
	+MBus24Bit_Send8BitValueFromA

	; Address
	+MBus24Bit_SendLE24BitValue .address
	; Inv scales
	+MBus24Bit_Send8BitValue $20
	+MBus24Bit_Send8BitValue $20
	; Stride
	+MBus24Bit_Send8BitValue .tileStride-1
}
