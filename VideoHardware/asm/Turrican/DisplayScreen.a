!zn
DisplayScreen
	jsr Bus24Bit_Init

	jsr Video_DisableDisplay
	jsr Video_InitDisplaySpritesScrollsBackground
	lda #kVideo_EnableDisplay_Enable | kVideo_EnableDisplay_Tiles_Enable
	; Allows decompression to be seen on the screen
;	jsr Video_EnableDisplay

	jsr DisplayData

	jsr Video_SetAddressVideoPriorityRegister
	+MBus24Bit_VideoLayer_EmitPriority_NearToFar_A kBus24Bit_VideoLayer_Priority_Chars , kBus24Bit_VideoLayer_Priority_Mode7 , kBus24Bit_VideoLayer_Priority_Sprites , kBus24Bit_VideoLayer_Priority_Tiles

	inc VIC2BorderColour

	jsr CopySpriteFrameData
	jsr CopyMode7Data
	; Copy the flags only once, it also helps to copy the flags for mode7 while the display is disabled or definitely in the vsync
	; If the flags init happens during the frame, the vertical counters will not be initialised properly compared to subsequent frames
	lda Mode7Regs_flags
	sta CIA2PortBRS232
	jsr CopyTileScrolls

	lda #kVideo_EnableDisplay_Enable | kVideo_EnableDisplay_Tiles_Enable | kVideo_EnableDisplay_Tiles_BorderXShrink | kVideo_EnableDisplay_Tiles_BorderYShrink
	jsr Video_EnableDisplay

	jsr Bus24Bit_Reset
	inc VIC2BorderColour
	rts


!zn	
Mode7Regs

Mode7Regs_scale_dX
	+MLittleEndian24Bit $100
Mode7Regs_scale_dXY
	+MLittleEndian24Bit 0
Mode7Regs_scale_dY
	+MLittleEndian24Bit $100
Mode7Regs_scale_dYX
	+MLittleEndian24Bit 0

Mode7Regs_xpos
	+MLittleEndian24Bit -10240
Mode7Regs_ypos
	+MLittleEndian24Bit -10240

	!by 0
	!by 0

Mode7Regs_backgroundColour !by 0
Mode7RegsSize = * - Mode7Regs
Mode7Regs_flags !by $1f



TileScrollX
	+MLittleEndian16Bit 0
TileScrollY
	+MLittleEndian16Bit 0

CopyTileScrolls
	jsr Video_SetAddressTileScrollRegisters
;	jsr Video_SetAddressCharScrollRegisters
!for .i , 4 {
	lda TileScrollX + .i-1
	sta CIA2PortBRS232
}
	rts

CopyMode7Data
	jsr Video_SetAddressMode7
!for .i , Mode7RegsSize {
	lda Mode7Regs + .i-1
	sta CIA2PortBRS232
}
	rts
