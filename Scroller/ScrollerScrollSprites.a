!ifdef Scroller_EnableScrollSpritesSpeedCode {

!zn
Scroller_ScrollSprites
	lda scrollerRealMovementXPixels
	bne .someX1
	jmp .noX1
.someX1
	bpl .posX1
	jmp .negX1

.posX1

!for .ii , Multiplex_items {
!set .i = .ii - 1
!ifdef Animation_ActiveNonZeroEnabled {
	lda Animation_ActiveNonZero + .i
	beq +
}
	lda AnimationXPosLo + .i
	clc
	adc scrollerRealMovementXPixels
	sta AnimationXPosLo + .i
	bcc +
	; This ensures the 9bit wraparound happens correctly
	lda AnimationXPosHi + .i
	eor #1
	sta AnimationXPosHi + .i
+
}
	jmp .noX1

.negX1

!for .ii , Multiplex_items {
!set .i = .ii - 1
!ifdef Animation_ActiveNonZeroEnabled {
	lda Animation_ActiveNonZero + .i
	beq +
}
	lda AnimationXPosLo + .i
	clc
	adc scrollerRealMovementXPixels
	sta AnimationXPosLo + .i
	bcs +
	; This ensures the 9bit wraparound happens correctly
	lda AnimationXPosHi + .i
	eor #1
	sta AnimationXPosHi + .i
+
}

.noX1

	lda scrollerRealMovementYPixels
	bne .someY1
	jmp .noY1
.someY1
!for .ii , Multiplex_items {
!set .i = .ii - 1
!ifdef Animation_ActiveNonZeroEnabled {
	lda Animation_ActiveNonZero + .i
	beq +
}
	lda AnimationYPos + .i
	clc
	adc scrollerRealMovementYPixels
	sta AnimationYPos + .i
+
}

.noY1

	rts

} else {
!zn
Scroller_ScrollSprites
	; Note: Usually it is better to optimise this by combining animation movement and screen scroll movement together.
	lda scrollerRealMovementXPixels
	beq .noX1
	bmi .negX1
	ldx #Multiplex_items-1
.spx1
!ifdef Animation_ActiveNonZeroEnabled {
	lda Animation_ActiveNonZero,x
	beq .noxMSB1
}
	lda AnimationXPosLo,x
	clc
	adc scrollerRealMovementXPixels
	sta AnimationXPosLo,x
	bcc .noxMSB1
	; This ensures the 9bit wraparound happens correctly
	lda AnimationXPosHi,x
	eor #1
	sta AnimationXPosHi,x
.noxMSB1
	dex
	bpl .spx1
	jmp .noX1

.negX1
	ldx #Multiplex_items-1
.spx2
!ifdef Animation_ActiveNonZeroEnabled {
	lda Animation_ActiveNonZero,x
	beq .noxMSB2
}
	lda AnimationXPosLo,x
	clc
	adc scrollerRealMovementXPixels
	sta AnimationXPosLo,x
	bcs .noxMSB2
	; This ensures the 9bit wraparound happens correctly
	lda AnimationXPosHi,x
	eor #1
	sta AnimationXPosHi,x
.noxMSB2

	dex
	bpl .spx2

.noX1

	lda scrollerRealMovementYPixels
	beq .noY1
	ldx #Multiplex_items-1
.spy1
!ifdef Animation_ActiveNonZeroEnabled {
	lda Animation_ActiveNonZero,x
	beq .skipEntry
}
	lda AnimationYPos,x
	clc
	adc scrollerRealMovementYPixels
	sta AnimationYPos,x

.skipEntry
	dex
	bpl .spy1
.noY1

	rts
}
