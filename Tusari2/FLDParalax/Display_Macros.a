!macro DisplayGameScreenClouds {
	+MVIC2MemorySetup_ScreenChar_A GameScreen2 , GameChars
	sta VIC2MemorySetup
	lda #CIA2PortASerialBusVICBank_Bank1
	sta CIA2PortASerialBusVICBank

	lda #VIC2Colour_LightGrey
	sta VIC2ExtraBackgroundColour1
	lda #VIC2Colour_Grey
	sta VIC2ExtraBackgroundColour2

	+MByteValueToAddress_A VIC2Colour_LightBlue , VIC2ScreenColour
}

!macro DisplayGameScreenTop {
	lda #VIC2Colour_Orange
	sta VIC2ExtraBackgroundColour1
	lda #VIC2Colour_Red
	sta VIC2ExtraBackgroundColour2
}

!macro DisplayGameScreenMiddle {
	lda #VIC2Colour_DarkGrey
	sta VIC2ExtraBackgroundColour1
	lda #VIC2Colour_Brown
	sta VIC2ExtraBackgroundColour2
}

!macro DisplayGameScreenBottom {
	lda #VIC2Colour_Grey
	sta VIC2ExtraBackgroundColour1
	lda #VIC2Colour_Brown
	sta VIC2ExtraBackgroundColour2
}

!macro DoFLD {
	cpx #0
	beq .o1
.l3
	lda VIC2Raster
.l2
	cmp VIC2Raster
	beq .l2
	and #$07
	sta VIC2ScreenControlV
	dex
	bpl .l3
.o1
}

!macro DoWaitNoC_A {
.l7
	cmp VIC2Raster
	bcs .l7
}


!macro MProcessCharLine .line {
	lda GameScreen2 + (.line * 40) + 39
	pha
!for .ii , 39 {
!set .i = .ii - 1
	lda GameScreen2 + (.line * 40) + 38 - .i
	sta GameScreen2 + (.line * 40) + 39 - .i
}
	pla
	sta GameScreen2 + (.line * 40)
}
!macro MProcessCharLineNeg .line {
	lda GameScreen2 + (.line * 40)
	pha
!for .ii , 39 {
!set .i = .ii - 1
	lda GameScreen2 + (.line * 40) + 1 + .i
	sta GameScreen2 + (.line * 40) + .i
}
	pla
	sta GameScreen2 + (.line * 40) + 39
}

!macro MProcessCharLineColour .line {
	lda GameScreen2 + (.line * 40) + 39
	pha
	lda COLOURRAM + (.line * 40) + 39
	pha
!for .ii , 39 {
!set .i = .ii - 1
	lda GameScreen2 + (.line * 40) + 38 - .i
	sta GameScreen2 + (.line * 40) + 39 - .i
	lda COLOURRAM + (.line * 40) + 38 - .i
	sta COLOURRAM + (.line * 40) + 39 - .i
}
	pla
	sta COLOURRAM + (.line * 40)
	pla
	sta GameScreen2 + (.line * 40)
}
!macro MProcessCharLineColourNeg .line {
	lda GameScreen2 + (.line * 40)
	pha
	lda COLOURRAM + (.line * 40)
	pha
!for .ii , 39 {
!set .i = .ii - 1
	lda GameScreen2 + (.line * 40) + 1 + .i
	sta GameScreen2 + (.line * 40) + .i
	lda COLOURRAM + (.line * 40) + 1 + .i
	sta COLOURRAM + (.line * 40) + .i
}
	pla
	sta COLOURRAM + (.line * 40) + 39
	pla
	sta GameScreen2 + (.line * 40) + 39
}
