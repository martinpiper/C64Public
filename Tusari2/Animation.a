!zn

!source "SpritePosToScreenPosMem.a"
!source "SpritePosToScreenPosMem2.a"

Animation_FramesLo
	!by <AnimationFrames_MainCharLegsStandLeft
	!by <AnimationFrames_MainCharTorsoStandLeft
	!fill Multiplex_items-2 , 0

Animation_FramesHi
	!by >AnimationFrames_MainCharLegsRight
	!by >AnimationFrames_MainCharTorsoRight
	!fill Multiplex_items-2 , 0


Animation_TickLo
	!fill Multiplex_items , 0
Animation_TickHi
	!fill Multiplex_items , 0

Animation_DeleteLo
	!fill Multiplex_items , 0
Animation_DeleteHi
	!fill Multiplex_items , 0


Animation_Index
	!fill Multiplex_items , 0

Animation_ActivationObjectIndex
	!fill Multiplex_items , 0

; Can be used for anything, but mostly health
Animation_GeneralStorage0
	!fill Multiplex_items , 0
Animation_GeneralStorage1
	!fill Multiplex_items , 0
Animation_GeneralStorage2
	!fill Multiplex_items , 0

AnimationInit2
	; Setup the player position
	lda #136
	sta AnimationXPosLo
	lda #144
	sta AnimationYPos
	lda #0
	sta AnimationXPosHi

	lda #$ff
	sta VIC2SpriteMulticolour
	lda #VIC2Colour_DarkGrey
	sta VIC2ExtraSpriteColour1
	lda #VIC2Colour_Yellow
	sta VIC2ExtraSpriteColour2

	lda #VIC2Colour_LightRed
	sta AnimationSpriteColour
	sta AnimationSpriteColour+1

	; Set map scroll position
	+MByteValueToAddress_A 71 , scrollMapHX
;	+MByteValueToAddress_A 41 , scrollMapHX
	+MByteValueToAddress_A 0 , scrollMapLX
	+MByteValueToAddress_A 24 , scrollMapHY
;	+MByteValueToAddress_A 0 , scrollMapHY
	+MByteValueToAddress_A 0 , scrollMapLY
	+MByteValueToAddress_A 4 , frameScrollX
	+MByteValueToAddress_A 4 , frameScrollY
	jsr ScrollerUpdateRealCoordinates
	jsr ActivationObjectInit

	+MWordValueTo_AX Scroller_RunJumpDemo_timerPos
	jsr ScoreSetAddress
	ldy #Scroller_RunJumpDemo_timerLen
	jsr ScoreInit

	rts



Animation_FindFreeSlot
	ldy #Multiplex_items-1
.ff1
	lda Animation_FramesHi,y
	beq .foundFree
	dey
	; Never consider the two player animation slots
	cpy #1
	bne .ff1
	sec
	rts
.foundFree
	clc
	rts


; For Animation_UpdateTick
.discard
	lda Animation_DeleteHi,x
	beq .noDeleteFunc
	sta .smDelete+2

	lda Animation_DeleteLo,x
	sta .smDelete+1

.smDelete	jsr $1234

.noDeleteFunc

	lda #0
	sta Animation_FramesHi,x
	jmp .nextSlot

PlayerXPosShifted !by 0

Animation_UpdateTick
	; Calculate the shifted player pos once
	lda AnimationXPosHi
	ror
	lda AnimationXPosLo
	ror
	sta PlayerXPosShifted

	; Loop through animation frames
	ldx #Multiplex_items-1
.an1
	lda Animation_FramesHi,x
	beq .nextSlot
	sta tempWork1

	; Never, ever, discard the player animations in the first two slots
	cpx #2
	bcc .noDiscard

	lda AnimationYPos,x
	cmp #Scroller_DiscardMinSpriteYPos-16
	bcc .discard
!set Multiplex_DiscardSpritesYPosSafe = Multiplex_DiscardSpritesYPos+16
!if Multiplex_DiscardSpritesYPosSafe > VIC2SpriteYBorderBottom {
	!set Multiplex_DiscardSpritesYPosSafe = VIC2SpriteYBorderBottom
}
	cmp #Multiplex_DiscardSpritesYPosSafe
	bcs .discard

.testXPos
	lda AnimationXPosHi,x
	beq .noDiscard
	lda AnimationXPosLo,x
	cmp #<(VIC2SpriteXBorderRight+32+16)
	bcs .discard

.noDiscard
	lda Animation_FramesLo,x
	sta tempWork0
	ldy Animation_Index,x
	lda (tempWork0),y
	bne .gotFrame
	; 0 = end of animation sequence
	iny
	lda (tempWork0),y
	cmp #kEndAnim_Delete
	beq .discard
	; Otherwise it's kEndAnim_Loop
	lda #0
	tay
	sta Animation_Index,x
	lda (tempWork0),y
	sta AnimationSpriteFrame,x
	jmp .doTick
.gotFrame
	sta AnimationSpriteFrame,x
	iny
	tya
	sta Animation_Index,x

.doTick
	lda Animation_TickHi,x
	beq .noTickFunc
	sta .smTick+2

	lda Animation_TickLo,x
	sta .smTick+1

	; Preserve X
	stx .smx+1
.smTick	jsr $1234
.smx ldx #0

.noTickFunc

.nextSlot
	dex
	bpl .an1

	; MPi: TODO: Process movement for everything else

	rts



!zn
; Morphs to a particular animation if it is different to what is currently there. This resets the frame index if the morph happens.
; Entry:
; A/X = lo/hi animation header
; Y = Animation index
Animation_MorphToIfNeeded
	pha
	cmp Animation_FramesLo,y
	bne .l1

	txa
	cmp Animation_FramesHi,y
	bne .l2

	pla
	rts

.l1
	txa
.l2
	sta Animation_FramesHi,y
	pla
	sta Animation_FramesLo,y
.commonInit
	lda #0
	sta Animation_Index,y
	rts

; Entry:
; A/X = lo/hi animation header
; Y = Animation index
Animation_ForceSet
	sta Animation_FramesLo,y
	sta .smFrame+1
	txa
	sta Animation_FramesHi,y
	sta .smFrame+2
	; Init the first frame
.smFrame lda $1234
	sta AnimationSpriteFrame,y
	jmp .commonInit
